#;;; -*- Encoding: utf-8 -*-

set char-encoding UTF-8
set quit-on-fail ON

define atomize [
       ου -> {ου}, αι -> {αι}, οι -> {οι}, ει -> {ει}, ευ -> {ευ}, εὐ -> {εὐ},
       ού -> {ού}, αί -> {αί}, οί -> {οί}, εί -> {εί}, εύ -> {εύ}, εὔ -> {εὔ},
       οῦ -> {οῦ}, αῖ -> {αῖ}, οῖ -> {οῖ}, εῖ -> {εῖ}, εἱ -> {εἱ},
       αἰ -> {αἰ}, αἴ -> {αἴ}, αἱ -> {αἱ}, εἷ -> {εἷ},
       "·" -> 0
       ] ;

define symbolize [
       {ου} -> ου, {αι} -> αι, {οι} -> οι, {ευ} -> ευ, {ει} -> ει,
       {ού} -> ού, {αἰ} -> αἰ, {αἱ} -> αἱ, {εὐ} -> εὐ
       ] ;

define Pres "@U.Tense.Pres@" ;
define Fut "@U.Tense.Fut@" ;
define Aor "@U.Tense.Aor@" ; # "@U.Aug.-@" ;
define Aor2 Aor "@U.Type.Aor2@" ;
define Aor1 Aor "@U.Type.Aor1@" ;
define FutMed "@U.FutVoice.Med@" ;
define Med "@U.Voice.Med@" ;
define PresFut Pres | Fut ;
define PresFutAor1 Pres | Fut | Aor1 ;
define FutAor Fut | Aor1 ;
define PresAor1 Pres | Aor1 ;

# non-contract verbs ending in vowel get ":"
define Verb symbolize.i .o. [
        PresFut [ {σπευδω} |
        {παρασκευαζω} |
        {κομιζω} |
        {τιμαω} |
        {δηλοω} ] |
        PresFut {μενω} | Aor1 {μενω}:{μειν-} |
        PresFutAor1 {φιλεω} |
        PresFutAor1 {λυω} |
        Pres {πλε:ω}      | Fut {πλε:ω}:[{πλευ-}|{πλευσε:-}] | Aor1 {πλε:ω}:{πλευ-} |
        Pres {ἀποθνῃσκω}  | Fut Med {ἀποθνῃσκω}:{ἀποθανε:-} | Aor2 {ἀποθνῃσκω}:{ἀπο·θαν-} |
        Pres {ἀφικνεομαι} | [Fut | Aor2] {ἀφικνεομαι}:{ἀφ·ικ-} |
        Pres {ἐσθιω}      | Fut Med {ἐσθιω}:{ἐδ#-} | Aor2 {ἐσθιω}:{φαγ-} |
        PresFut {ἀγω}     | Aor2 {ἀγω}:{ἀγαγ-} |
        PresFut {λειπω}   | Aor2 {λειπω}:{λιπ-} |
        Pres {αἰρω}       | Fut {αἰρω}:{ἀρε:-} |
        PresFut {αἱρεω}   | Aor2 "@U.Aug.ει@" "@P.Aug.-@" {αἱρεω}:{ἑλ-} | # still wrong: εἵλον
        Pres {βαλλω}      | Fut {βαλλω}:{βαλε:-} | Aor2 {βαλλω}:{βαλ-} |
        Pres {εὐρισκω}    | Fut {εὐρισκω}:{εὐρε-} | Aor2 {εὐρισκω}:{εὐ-} |
        Pres {ἐχω}        | Fut {ἐχω}:[{ἑχ-}|{σχε-}] | Aor2 {ἐχω}:{σχ} |
        Pres {ἐγειρω}     | Fut {ἐγειρω}:{ἐγερε:-} |
        Pres {ἐλαυνω}     | Fut {ἐλαυνω}:{ἐλα#-} |
        Pres {ἀποκτεινω}  | Fut {ἀποκτεινω}:{ἀποκτενε:-} |
        Pres {καμνω}      | Fut Med {καμνω}:{καμε:-} | Aor2 {καμνω}:{καμ-} |
        Pres {λαμβανω}    | Fut Med {λαμβανω}:{ληβ-} | Aor2 {λαμβανω}:{λαβ-} |
        Pres {μανθανω}    | Fut Med {μανθανω}:{μαθε-} | Aor2 {μανθανω}:{μαθ-} |
        Pres {πασχω}      | Fut Med {πασχω}:{πενθ-} | Aor2 {πασχω}:{παθ-} |
        Pres {βαινω}      | Fut Med {βαινω}:{βη-} |
        Pres {γιγνωσκω}   | Fut Med {γιγνωσκω}:{γνω-} |
        Pres {ὁραω}       | Fut Med {ὁραω}:{ὀπ-}      | Aor2 "@U.Aug.ει@" "@P.Aug.-@" {ὁραω}:{ἰδ-} |
        Pres {τρεχω}      | Fut Med {τρεχω}:{δραμε:-} | Aor2 {τρεχω}:{δραμ:-} |
        Pres {πινω}       | Fut Med {πινω}:{πι#-}     | Aor2 {πινω}:{πι-} | # no σ!
        Pres {πιπτω}      | Fut Med {πιπτω}:{πεσε:-}  | Aor2 {πιπτω}:{πεσ-} |
        PresFut {λεγω}    | Aor2 "@U.Aug.ει@" "@P.Aug.-@" {λεγω}:{ἐπ-} |
        PresFut FutMed {φευγω} | Aor2 {φευγω}:{φυγ-} |
        Pres Med {μαχομαι} | Fut Med {μαχομαι}:{μαχε:-} |
        Pres Med {γιγνομαι} | Fut Med {γιγνομαι}:{γενε-} | Aor2 Med {γιγνομαι}:{γεν-} |
        PresFutAor1 Med {ἀπο·κρινομαι} |
        PresAor1 {ἐγειρω} | Fut {ἐγερε-} |
        PresFutAor1 FutMed {βλεπω}  |
        PresFutAor1 FutMed {βοαω}  |
        PresFutAor1 FutMed {ἀκουω} |
        PresFutAor1 {διωκω} |
        PresFut FutMed {βαδιζω} | Aor1 {βαδιζω}:{βαδι-} |
        PresFutAor1 FutMed {θαυμαζω} |
        PresFutAor1 Med ("@U.Aug.ει@" "@P.Aug.-@") {ἐργαζομαι} |
        PresFut Med {δεχομαι}
       ] .o. symbolize ;

define PrimAct [
       "+1Sg":{ω}     "@U.Num.1Sg@" |
       "+2Sg":[ει ς]   "@U.Num.2Sg@" |
       "+3Sg":[ει]    "@U.Num.3Sg@" |
       "+1Pl":{ομεν}  "@U.Num.1Pl@" |
       "+2Pl":{ετε}   "@U.Num.2Pl@" |
       "+3Pl":[ου {σι}(ν)] "@U.Num.3Pl@"
       ] ;

define PrimMed [
       "+1Sg":[μ αι] "@U.Num.1Sg@" |
       "+2Sg":[{.} σ αι] "@U.Num.2Sg@" |
       "+3Sg":[τ αι] "@U.Num.3Sg@" |
       "+1Pl":{.μεθα} "@U.Num.1Pl@" |
       "+2Pl":{σθε} "@U.Num.2Pl@" |
       "+3Pl":[{ντ}αι] "@U.Num.3Pl@"
       ] ;

define SecAct [
       "+1Sg":ν     "@U.Num.1Sg@" |
       "+2Sg":ς     "@U.Num.2Sg@" |
       "+3Sg":(ν)   "@U.Num.3Sg@" |
       "+1Pl":{μεν} "@U.Num.1Pl@" |
       "+2Pl":{τε}  "@U.Num.2Pl@" |
       "+3Pl":ν     "@U.Num.3Pl@"
       ] ;

define SecMed [
       "+1Sg":{μην} "@U.Num.1Sg@" |
       "+2Sg":{.σο} "@U.Num.2Sg@" |
       "+3Sg":{το} "@U.Num.3Sg@" |
       "+1Pl":{.μεθα} "@U.Num.1Pl@" |
       "+2Pl":{σθε} "@U.Num.2Pl@" |
       "+3Pl":{ντο} "@U.Num.3Pl@"
       ] ;

define SecActSig [
       "+1Sg":0     "@U.Num.1Sg@" |
       "+2Sg":ς     "@U.Num.2Sg@" |
       "+3Sg":(ν)   "@U.Num.3Sg@" |
       "+1Pl":{μεν} "@U.Num.1Pl@" |
       "+2Pl":{τε}  "@U.Num.2Pl@" |
       "+3Pl":ν     "@U.Num.3Pl@"
       ] ;

# for easier testing
define PrimActx [
       "+1Sg":{ω}     "@U.Num.1Sg@"
       ] ;

define PrimMedx [
       "+1Sg":[μ αι] "@U.Num.1Sg@"
       ] ;



define ThemV [
       0:ο "@U.Num.1Sg@" |
       0:ε "@U.Num.2Sg@" |
       0:ε "@U.Num.3Sg@" |
       0:ο "@U.Num.1Pl@" |
       0:ε "@U.Num.2Pl@" |
       0:ο "@U.Num.3Pl@"
       ] ;

define Contractions [
     [ [ε "." σ αι] -> ει,
       [ε "." σ ο] -> ου,
       [έ (":") "." σ ] -> έ ".", # δραμοῦμαι
       [ε ":" "." σ ] -> ε "."] # μενούμεθα
     .o.
     [ [ ":" "." σ ] -> 0 || [ λ | ρ | μ | ν ] _ ] # asig 1. Aor: ἔμεινα
     .o.
     [ [ά "." ει] -> ᾷ,
       [ά "." ου] -> ῶ,
       [έ (":") "." ει] -> εῖ,
       [έ "." ου] -> οῦ,
       [ό "." ει] -> οῖ,
       [ό "." ου] -> οῦ ]
       .o.
     [ [α "." ό "."] -> ώ,
       [ε "." ό "."] -> ού
     ]
       .o.
     [ [ά "." ω] -> ῶ,
       [ά "." ο] -> ῶ,
       [α "." ό] -> ῶ,
       [ά "." ε] -> ᾶ,
       [έ "." ω] -> ῶ,
       [έ "." ο] -> οῦ,
       [έ (":") "." ε] -> εῖ,
       [ό "." ω] -> ῶ,
       [ό "." ο] -> οῦ,
       [ό "." ε] -> οῦ
       ]
      .o. [ [":" | "."] -> 0 ]
     ] ;


define Short [ α | ε | ι | ο | υ | ου | αι | ἐ | ἑ ] ;
define Long  [ η | ω | ει | αἰ | αἱ | ῃ | ευ ] ; 

define Vowel Short | Long ;

# "#" marks thematic 2nd Aor Med Inf (11.2 note)
define Consonant \[Vowel | "#"] ;

define Accented [ ά | έ | ί | ό | ύ | ή | ώ | ού | αί | εί | ὄ | ἄ | ἔ | ὔ | ἤ | ὤ
                | αἴ | εὔ | ῄ | ἥ | ἕ | εἶ | εἵ | εύ ] ;

define Character Short | Long | Accented ;

define RegressiveAccent [
          α -> ά, ε -> έ, ι -> ί, ο -> ό, υ -> ύ, η -> ή, ω -> ώ, ου -> ού, αι -> αί, ει -> εί,
          ὀ -> ὄ, ἀ -> ἄ, ἐ -> ἔ, ἑ -> ἕ, ευ -> εύ,
          ἠ -> ἤ, ὠ -> ὤ, ὐ -> ὔ, αἰ -> αἴ, αἱ -> αἵ, ῃ -> ῄ, εὐ -> εὔ, εἰ -> εἶ, εἱ -> εἵ,
          ἡ -> ἥ
          || _ Consonant* (Vowel Consonant* | Vowel Consonant* [ Short | "#" ] Consonant*) .#. ]
        .o. 
        [ ά -> α, έ -> ε, ί -> ι, ό -> ο, ύ -> υ, ή -> η, ώ -> ω, ού -> ου, αί -> αι, εί -> ει,
          ὄ -> ὀ, ἄ -> ἀ, ἔ -> ἐ, ἕ -> ἑ, εύ -> ευ,
          ἤ -> ἠ, ὤ -> ὠ, ὔ -> ὐ, αἴ -> αἰ, αἵ -> αἱ, ῄ -> ῃ, εὔ -> εὐ, εἶ -> εἰ, εἵ -> εἱ, 
          ἥ -> ἡ 
          || Accented ?*  _ ]
        .o.
        [ "#" -> 0 ] ;

define SigmaAssimilations [
       [ {#.} σ -> "."  ] # exeptions like πίομαι
     .o.
       [ ι ζ "." σ -> ι ε "." ] # κομίζω -> κομιῶ
     .o.
     [ π "." σ -> ψ,
       φ "." σ -> ψ,
       β "." σ -> ψ,
       γ "." σ -> ξ,
       κ "." σ -> ξ,
       χ "." σ -> ξ,
       [δ | θ | ζ] "." σ -> σ ] 
       ]
      .o.
      [ {ενσ} -> ει σ # πείσομαι
      ] ;

define lengthenContractStem [
       ε -> η, α -> η, ο -> ω || _ .#. ] ;

define getStem [
       [ ω -> 0 || _ .#. ] .o.
       [ ο μ αι -> 0 || _ .#. ] .o.
       [ "-" -> 0 || _ .#. ]
       ] ;

# 10.5
define asigmaticLiqNasStemFut [ λ | ρ | μ | ν ] -> ... ε ":" || _ .#. ;
# 12.3
define asigmaticLiqNasStemAor [ λ | ρ | μ | ν ] -> ... ":" || _ .#. ;

define Present [
       [ "@U.Tense.Pres@" Verb
          .o. getStem
        ]
          0:"." 
       [
         [ "@D.Voice.Med@" PrimAct "+Pres":0 "+Act":0 ]
       | [ ThemV PrimMed "+Pres":0 "+Med":0 ]
       | [ "@D.Voice.Med@" ["+Inf" "+Pres" "+Act"]:[ει ν] ] 
       | [ ["+Inf" "+Pres" "+Med"]:[ε σ θ αι] ] 
       ]
       ] ;

define Future [
       [ "@U.Tense.Fut@" Verb .o. getStem .o. asigmaticLiqNasStemFut .o. lengthenContractStem ]
       0:{.σ}
       [
         [ "@D.Voice.Med@" "@D.FutVoice.Med@" PrimAct "+Fut":0 "+Act":0 ]
       | [ ThemV PrimMed "+Fut":0 "+Med":0 ]
       | [ "@D.Voice.Med@" "@D.FutVoice.Med@" ["+Inf" "+Fut" "+Act"]:[ει ν] ] 
       | ["+Inf" "+Fut" "+Med"]:[{εσθ}αι] 
       ]
       ] .o. SigmaAssimilations
       ;

define resolveAugment [
       [ "·" -> Aug ] .o. [ Aug -> 0 || _ ?* Aug ] .o. # place augment behind prefix
       [ Aug ἀ -> ἠ ] .o.
       [ Aug ἁ -> ἡ ] .o.
       [ Aug ἑ -> ἡ ] .o.
       [ Aug ἐ -> ἠ ] .o.
       [ Aug ι -> ι ] .o.
       [ ο Aug -> ε ] .o.
       [ Aug -> ἐ ] .o.
       [ "Aug.ει" ἑ -> εἱ  ] .o.
       [ "Aug.ει" ἐ -> εἰ ] .o.
       [ "Aug.ει" ἰ -> εἰ ]
   #    [ "Aug.ει" ἑ -> εἷ ] .o.
   #    [ "Aug.ει" ἐ -> εἶ ] .o.
   #    [ "Aug.ει" ἰ -> εἶ ]
       ] ;

define Augment [ "@U.Aug.-@" 0:Aug | "@U.Aug.ει@" 0:"Aug.ει" ] ;

define Aorist1Sgm [
       [ "@U.Tense.Aor@" "@U.Type.Aor1@"
         [ "@U.Past.+@" Augment | "@U.Past.-@" ]
         Verb
         .o. getStem .o. asigmaticLiqNasStemAor .o. lengthenContractStem
        ]
          0:"." 
       [
         [ "@D.Voice.Med@" [ 0:{σα} "@N.Num.3Sg@" | 0:{σε} "@P.Num.3Sg@" ] SecActSig "+Aor":0 "+Act":0 "@U.Past.+@" ]
       | [  0:{σα} SecMed "+Aor":0 "+Med":0 "@U.Past.+@" ]
       # | [ "@D.Voice.Med@" ["+Inf" "+Aor" "+Act"]:[ει ν] "@U.Past.-@" ] 
       # | [ ["+Inf" "+Aor" "+Med"]:[ε σ θ αι "#"] "@U.Past.-@" ] 
       ]
       ] "@R.Aug.-@"
        ;

define Aorist2Th [
       [ "@U.Tense.Aor@" "@U.Type.Aor2@"
         [ "@U.Past.+@" Augment | "@U.Past.-@" ]
         Verb
         .o. getStem
        ]
          0:"." 
       [
         [ "@D.Voice.Med@" ThemV SecAct "+Aor":0 "+Act":0 "@U.Past.+@" ]
       | [ ThemV SecMed "+Aor":0 "+Med":0 "@U.Past.+@" ]
       | [ "@D.Voice.Med@" ["+Inf" "+Aor" "+Act"]:[ει ν] "@U.Past.-@" ] 
       | [ ["+Inf" "+Aor" "+Med"]:[ε σ θ αι "#"] "@U.Past.-@" ] 
       ]
       ] "@R.Aug.-@" ;

read regex Aorist1Sgm | Aorist2Th ;

eliminate flag Past
eliminate flag Num
eliminate flag Tense
eliminate flag Voice
eliminate flag FutVoice
eliminate flag Aug
eliminate flag Type

define Aorist ;
define Aorist Aorist .o. SigmaAssimilations .o. resolveAugment ;


read regex [ RegressiveAccent.i
       .o.
       [ 0 <- ":" ]
       .o.
       [ Present | Future | Aorist ]
       .o.
       RegressiveAccent ] ;

eliminate flag Num
eliminate flag Tense
eliminate flag Voice
eliminate flag FutVoice
eliminate flag Aug

define Verbs

read regex atomize.i .o. Verbs .o. Contractions
            .o. atomize ;


save stack greek.fst

pop stack

# eof